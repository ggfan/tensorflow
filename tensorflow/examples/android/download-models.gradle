/*
 * download-models.gradle
 *     Downloads model files from ${MODEL_URL} into application's asset folder
 * Input:
 *     project.ext.TMP_DIR: absolute path to hold downloaded zip files
 *     project.ext.ASSET_DIR: absolute path to save unzipped model files
 * Output:
 *     3 model files will be downloaded into given folder of ext.ASSET_DIR
 */
// hard coded model files
def models = ['inception5h.zip',
              'mobile_multibox_v1a.zip',
              'stylize_v1.zip']

// model's web address: assuming all model files are saved under this dir
def MODEL_URL = 'https://storage.googleapis.com/download.tensorflow.org/models'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.2.0'
    }
}

import de.undercouch.gradle.tasks.download.Download
task downloadFile(type: Download){
    for (f in models) {
        src "${MODEL_URL}/" + f
    }
    dest new File(project.ext["TMP_DIR"])
    overwrite true
}
task downloadModels(type: Copy) {
    for (f in models) {
        from zipTree(project.ext.TMP_DIR + '/' + f)
    }

    into file(project.ext.ASSET_DIR)
    fileMode = 0666
    exclude '**/LICENSE'

    dependsOn downloadFile
}

